

!!! Adding two dice

[[[
testAddTwoDice

	| hd |
	hd := (Die withFaces: 6) + (Die withFaces: 6).
	self assert: hd dice size = 2.
]]]	

[[[	
Die >> + aDie
	
	^ DieHandle new 
		addDie: self; 
		addDie: aDie; yourself
]]]


!!! Preparing the mix

[[[
Die >> + aDie	
	^ aDie sumFromDie: self
]]]

[[[
Die >> sumFromDie: aDie

	^ DieHandle new 
		addDie: self; 
		addDie: aDie; yourself
]]]


[[[
DieHandle >> sumFromHandle: aDieHandle
	| handle |
	handle := self class new.
	self dice do: [ :each | handle addDie: each ].
	aDieHandle dice do: [ :each | handle addDie: each ].
	^ handle
]]]


[[[
DieHandle >> + aDieHandle
	^ aDieHandle sumFromHandle: self
]]]

Up until here we did not change much and all the tests should run. 



!!! Adding an handle and a die 

[[[
DieHandleTest >> testAddingAnHandleWithADie
	| handle res |
	handle := DieHandle new
		addDie: (Die faces: 6);
		addDie: (Die faces: 10);
		yourself.
	res := handle + (Die withFaces: 20).
	self assert: res diceNumber = 3
]]]

[[[
Die >> sumFromHandle: aDieHandle
	
	| hd |
	hd := DieHandle new.
	hd addDie: self.
	aDieHandle dice do: [ :each | hd addDie: each ].
	^ hd
]]]


!!! Adding a die and an handle
[[[
DieTest >> testAddingADiceAndHandle

	| hd |
	hd := (Die withFaces: 6) + 
	(DieHandle new addDie: (Die withFaces: 6); yourself).
	self assert: hd dice size = 2.
]]]

[[[
DieHandle >> sumFromDie: aDie
	| handle |
	handle := self class new.
	self dice do: [ :each | handle addDie: each ].
	handle addDie: aDie.
	^ handle
]]]



!!! For a better documentation

[[[
Die >> + aDicable	
	^ aDicable sumFromDie: self
]]]


[[[
DieHandle >> + aDieHandle
	^ aDieHandle sumFromHandle: self
]]]

